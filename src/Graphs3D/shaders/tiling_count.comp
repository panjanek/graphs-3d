#version 430

#pragma optimize(on)

layout(local_size_x = {LocalSizeX}) in;

struct ShaderConfig {
    int nodesCount;
    float dt;
    float sigma2;
    uint edgesCount;
    int useCells;
    float fieldSize;
    float cellSize;
    float maxDist;
    float gridSize;
    float damping;
    int trackedIdx;
    float maxForce;
    float amp;
    int cellCount;
    int totalCellCount;
    vec4 minBounds;
    vec4 maxBounds;
    int marker1;
    int marker2;
    float markerT;
};

struct Node
{
   vec4 position;
   vec4 velocity;
   vec4 prevForce;
   int player;
   int flags;
   int  cellIndex;
   int level;
   int leaf;
   int win;
   int parent;
   int _pad2;
};

layout(std140, binding = 0) uniform ConfigBuffer {
    ShaderConfig config;
};

layout(std430, binding = 1) buffer InputBuffer
{
    Node nodes[];
};

layout(std430, binding = 6) buffer CellCount {
    uint cellCount[];
};

int assign_cell(vec4 pos)
{
    vec3 local = pos.xyz - config.minBounds.xyz;

    // Convert to cell coordinates
    ivec3 cell = ivec3(floor(local / config.cellSize));

    // Clamp indices (not positions)
    cell = clamp(cell,
                 ivec3(0),
                 ivec3(config.cellCount - 1));

    return cell.x +
           cell.y * config.cellCount +
           cell.z * config.cellCount * config.cellCount;
}

void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >=0 && idx < config.nodesCount) 
    {
        int cellIndex = assign_cell(nodes[idx].position);
        nodes[idx].cellIndex = cellIndex;
        atomicAdd(cellCount[cellIndex], 1);
    }
}